# IXCLOUD Provider

~> This provider is currently in a beta stadium. If you use it and find something that is not working feel free to contact us.

This provider can be used to configure resources from the [ixCloud Portal](https://portal.ixcloud.ch)
using the provided api. It is intended for private customer of our company. Therefore the provider is of no use if you
aren't a customer of Inventx AG.

## Limitations

At the moment the provider cannot be installed automatically over the terraform registry.
Therefore you need to download the version manually and install it with the install scripts.
These will copy the binary to the correct location.

If you miss something please have a look first if there is a newer version. If you still miss
it, feel free to contact Inventx AG to discuss an additional implementation.

## Example Templates

For a start it is easiest to make an order in the ix.Cloud Portal and there use the *Show Code* functionality.
Then you get an terraform example of this specific order with the correct values.

## Access Token

For access our api you need an access token. The easiest way is to create a personal access token in your profile
in the ix.Cloud Portal.

You do not need to provide the token in the *.tf* file. Another way is to set the environment variable `IXCLOUD_TOKEN`
on your system you execute the terraform commands.

## Example Provider Usage (with token directly in tf file)

```terraform
provider "ixcloud" {
  token        = "Personal Token to Deploy your Resources"
  subscription = "81687e60-1df1-4daf-8b5b-dc48fdf8b400" # GUID of the subscription where you want to deploy it in
}
```

## Example of an Application Deployment

This example shows you how an application deployment can look like. It deploys a subnet, virtual machine and a database.

```terraform
terraform {
  required_providers {
    ixcloud = {
      source = "inventxag/ixcloud"
    }
  }
}

locals {
  cost_center = "MyCostCenter"
  owner       = "hans.mueller@inventx.ch"
  org_unit    = "IT"
}

provider "ixcloud" {
  subscription = "a852907a-05f4-458a-b98d-34c4837a663c"
}

resource "ixcloud_resourcegroup" "rg" {
  name  = "ApplicationX"
  owner = local.owner
}

resource "ixcloud_subnet" "net" {
  name                = "APP_X_DEV_SUBNET"
  cidr                = 28
  vrf                 = "PN-IX"
  owner               = local.owner
  cost_center         = local.cost_center
  resource_group      = ixcloud_resourcegroup.rg.name
  organizational_unit = local.org_unit
}

resource "ixcloud_virtualmachine" "vm" {
  name           = "APP-VM-01"
  sla            = "Bronze"
  hardware       = "P2-4"
  backup_days    = 0
  release_level  = "stable"
  os             = "L-AlmaLinux-9-Core"
  location       = "DCGA"
  storage_class  = "Standard"
  network        = ixcloud_subnet.net.name
  admin_pw       = "Supersecurepw1234!"
  owner          = local.owner
  resource_group = ixcloud_resourcegroup.rg.name
  tags           = ["Test"]
  addons         = {
    cis_hardening = true
    managed_os    = {
      patch_day  = 1
      patch_wave = "Wave 1"
    }
  }
}

resource "ixcloud_data_disk" "data1" {
  virtualmachine_name = ixcloud_virtualmachine.vm.name
  name                = "data1"
  disk_size_gb        = "42"
  lun                 = 2
}

resource "ixcloud_managedinstance_mysql" "database" {
  name                = "APP-DB"
  instance_type       = "MYSQL8019CE"
  network             = ixcloud_subnet.net.name
  sla                 = "Bronze"
  user                = "myuser"
  pw                  = "Supersecurepw1234!"
  location            = "DCGA"
  patch_wave          = "Wave 1"
  patch_day           = 1
  owner               = local.owner
  cost_center         = local.cost_center
  organizational_unit = local.org_unit
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Optional

- `host` (String)
- `subscription` (String)
- `token` (String, Sensitive)
